	TITLE CURSRV - CONTROLLER/UNIT/REGISTER SERVICE ROUTINES
	HISEG
	SUBTTL CONTROLLER SERVICE



	INTERN FNDCON,NXTCON
	EXTERN SMGR,CDMLST,CDMLSP
	INTERN FNDUNT,NXTUNT
	INTERN FNDREG,NXTREG,PRVREG
	INTERN FNDAUNT,NXTAUNT

AC=10
XR=11
RDBLNT=3
STK=17

FNDCON:	SETZM ,CONADR	;SET UP NXTCON TO GET FIRST CONTROLLER FIRST TIE
	PUSHJ STK,NXTCON	;GET NEXT CONTROLLER
	POPJ STK,	;NO MORE CONTROLLERS, FAIL
	MOVE AC,CONADR	;GET THE CONTROLLER NAME FROM THE CDM.
	MOVE AC,DNAM(AC)
	CAME AC,NAME		;COMPARE WITH NAME DESIRED
	JRST FNDCON+1	;NOT THE ONE WE WANT, TRY NEXT ONE
	JRST CPOPJ1	;GOT IT, TAKE SKIP RETURN.

NXTCON:	SKIPE ,CONADR	;GET FIRST CONTROLLER?
	JRST NTFSTC	;NO, GET NEXT CONTROLLER.
	MOVEI AC,CDMLST-1	;YES, INITIALIZE PTR INTO CDM LIST.
	MOVEM AC,CDMLSP
NTFSTC:	AOS ,CDMLSP	;INDEX POINTER IN CDM LIST.
	SKIPE AC,@CDMLSP	;GET CDM ADDRESS.  IS IT ZERO?
	AOS ,0(STK)	;NO, SET UP SKIP RETURN.
	MOVEM AC,CONADR
	POPJ STK,



	SUBTTL UNIT SERVICE

FNDUNT:	SETZM ,UNTADR	;SET UP NXTUNT TO GET FIRST UNIT FIRST TIME
	PUSHJ STK,NXTUNT	;GET NEXT UNIT
	POPJ STK,	;NO MORE UNITS, FAIL
	MOVE AC,UNTADR	;GET UNIT NAME FROM UCB.
	MOVE AC,UNAM(AC)
	CAME AC,NAME		;COMPARE WITH NAME DESIRED
	JRST FNDUNT+1	;NOT THE ONE WE WANT, TRY NEXT UNIT
	JRST CPOPJ1	;GIT IT, TAKE SKIP RETURN.

NXTUNT:	SKIPE XR,UNTADR	;GET FIRST UNIT OF CURRENT CONTROLLER?
	JRST NTFSTU	;NO,GET NEXT UNIT.
	MOVE XR,CONADR	;YES, GET CONTROLLER ADDRESS.
	HRRZ AC,DUCB(XR)	;GET ADDRESS OF FIRST UNIT FROM DDM.
	JRST .+2
NTFSTU:	HRRZ AC,UNXT(XR)	;GET ADDRESS OF NEXT UNIT FROM UCB.
	MOVEM AC,UNTADR	;STORE NEXT UNIT ADDRESS.
	JUMPN AC,GDRTN	;IF ADDRESS NOT ZERO, TAKE SKIP RETURN.
	POPJ STK,



	SUBTTL REGISTER SERVICE

FNDREG:	SETZM ,REGADR	;SET UP NXTREG TO GET FIRST REGISTER FIRST TIME
	PUSHJ STK,NXTREG	;GET NEXT REGISTER
	POPJ STK,	;NO MORE REGISTERS, FAIL
	MOVE AC,REGADR	;GET REGISTER NAME FROM RDB.
	MOVE AC,RNAM(AC)
	CAME AC,NAME		;COMPARE WITH DESIRED NAME
	JRST FNDREG+1	;NOT THE ONE WE WANT, TRY NEXT REGISTER
	JRST CPOPJ1	;GOT IT, TAKE SKIP RETURN.

NXTREG:	SKIPE AC,REGADR	;GET FIRST REGISTER?
	JRST NTFSTR	;NO, GET NEXT REGISTER.
	MOVE XR,CONADR	;YES, GET CONTROLLER ADDRESS.
	HLRZ AC,DRDB(XR)	;GET RDB LIST ADDRESS.
	JUMPE AC,REGFAL	;ANY RDB'S?  IF NOT, FAIL.
	JRST .+2	;OK, CHECK FOR NULL LIST.
NTFSTR:	ADDI AC,RDBLNT	;INDEX REGADR TO POINT AT NEXT RDB.
	MOVEM AC,REGADR
	SKIPE ,@REGADR	;END OF LIST?
	AOSA ,0(STK)	;NO, TAKE SKIP RETURN.
REGFAL:	SETZM ,REGADR	;YES, ZERO REGADR AND TAKE FAIL RETURN.
	POPJ STK,


PRVREG:	MOVE XR,CONADR	;ISOLATE ADDRESS OF FIRST RDB.
	HLRZ AC,DRDB(XR)
	CAMN AC,REGADR	;ARE WE POINTING AT FIRST?
	POPJ STK,	;YES, EXIT.
	MOVNI AC,RDBLNT	;NO, MOVE TO PREVIOUS
	ADDM AC,REGADR
	JRST CPOPJ1	;AND TAKE SKIP RETURN
;
;FNDAUNT:	FIND UNIT "NAME" BY SEARCHING ALL UNITS.
;NXTAUNT:	GO TO NEXT UNIT, STEPPING THROUGH ALL CONTROLLERS.
;
FNDAUNT:	CLEARM ,CONADR	;INITIALIZE CONADR.
	PUSHJ STK,NXTCON	;GO TO NEXT CONTROLLER.
	POPJ STK,	;NONE LEFT, FAIL.
	PUSHJ STK,FNDUNT	;LOOK FOR UNIT UNDER THAT CONTROLLER.
	JRST FNDAUNT+1	;NOT FOUND, GO TO NEXT CONTROLLER.
	JRST CPOPJ1	;FOUND, TAKE SKIP RETURN.
;
NXTAUNT:	SKIPE ,CONADR	;DO WE START AT THE TOP?
	JRST GTUNT	;NO. GET NEXT UNIT.
GTCON:	PUSHJ STK,NXTCON	;YES, GET FIRST(NEXT) CONTROLLER.
	POPJ STK,	;NONE LEFT, DONE.
	CLEARM ,UNTADR	;INITIALIZE UNIT STEPPING.
GTUNT:	PUSHJ STK,NXTUNT	;GET NEXT UNIT.
	JRST GTCON	;NONE, GO TO NEXT CONTROLLER.
GDRTN:
CPOPJ1:	AOS ,0(STK)	;FOUND, TAKE SKIP RETURN.
	POPJ STK,
;


	END

