	XALL
	XLIST
;
;COPYRIGHT 1973, APPLIED DATA RESEARCH, PRINETON, N.J.
;GRI-909 SIMULATOR DEFINITIONS.
;R.M. SUPNIK, 4/9/71
;
;DEFAULT DEFINITION MACROS:

IFNDEF VM,<	VM==SIXBIT/GRI909/>
IFE <VM-SIXBIT/GRI99/>,<	M99==1>

DEFINE OPT(NM,AA)	;OPTION DEFN
<IFNDEF NM,<	NM==0>
IFNDEF NM'A,<	NM'A==AA>>

DEFINE DEV(NM,AA,BB,CC)	;DEVICE DEFNS
<IFNDEF NM,<	NM==0>
IFNDEF NM'A,<	NM'A==AA>
IFNDEF NM'I,<	NM'I==BB>
IFNDEF NM'B,<	NM'B==^D'CC>
	NM'BIT==FLTMSK
	FLTMSK==FLTMSK+1
>
	FLTMSK==0	;INTERRUPT BIT ASSIGNMENT COUNTER

	TTI==1
	LSR==1
	TTO==1
	OPT AO,11	;ARITHMETIC OPERATOR
	OPT GR,30	;GENERAL REGISTERS
	OPT BSW,24	;BYTE SWAPPER
	OPT BPK,25	;BYTE PACKER
	OPT DAC,61	;DIGITAL TO ANALOG CONVERTER
	OPT MUX,64	;ADC MULTIPLEXOR
	OPT BIM,50	;BINARY INPUT MULTIPLEXOR
	OPT BOM,51	;BINARY OUTPUT MULTIPLEXOR
IFE AO&GR,<	EAO==0>	;ALLOW EAO ONLY IF BOTH GR AND AO PRESENT.
	OPT EAO,14	;EXTENDED ARITHMETIC OPERATOR
	OPT M99,22	;MODEL 99 HAS TWO EXTRA OPERATORS
			;22==INDEX REGISTER
			;23==ALTERNATE TRAP

	DEV HSR,76,22,3	;HIGH SPEED READER
	DEV HSP,76,17,2	;HIGH SPEED PUNCH
	DEV TTI,77,14,1	;KEYBOARD
	DEV TTO,77,11,0	;TELEPRINTER
	DEV RTC,75,100,11	;CLOCK
	DEV DSK,66,41,6	;DISK
	DEV ADC,65,52,12	;ANALOG TO DIGITAL CONVERTER
	DEV WIT1,60,23,13	;INTERVAL TIMER 1
	DEV WIT2,61,26,13	;INTERVAL TIMER 2
	DEV WIT3,62,31,13	;INTERVAL TIMER 3
	DEV GIC1,55,20,15	;GATED INPUT CARD 1
	DEV GOR1,56,44,14	;GATED OUTPUT REGISTER
IFNDEF PID,<	PID==0>		;PULSE INPUT DETECTOR
	DEV PID1,0,104,8	;PULSE INPUT DETECTOR INTERRUPTS
	DEV PID2,0,104,8
	DEV PID3,0,104,8
	DEV PID4,0,104,8
	DEV PID5,0,104,8
	DEV PID6,0,104,8
	DEV PID7,0,104,8
	DEV PID8,0,104,8
	PTR==HSR	;OLD NAMES
	PTP==HSP

IFNDEF CPU,<	CPU==4>	;CENTRAL MEMORY

	BOV=100000	;BITS IN MACHINE STATUS REG:  BUS OVFLO
	LINK=040000	;LINK
	SOV=000002	;SUM (SIGN) OVERFLOW
	AOV=000001	;ARITHMETIC OPERATOR OVERFLOW

	PWRF=1	;POWER FAILURE (LEFT HALF)

MGOR==1	;MAX# OF GOR'S
MGIC==1	;MAX# OF GIC'S
MWIT==3	;MAX# OF WIT'S
;
;AC ASSIGNMENTS.
;
	TM=0	;TIMER (IN MAJOR CYCLES)
	F=1	;PI LEVEL REQUEST FLAGS
	MA=2	;MEMORY ADDRESS
	SC=3	;SEQUENCE COUNTER
	BR=4	;BUS BUFFER REGISTER
	IR=5	;INSTRUCTION REGISTER
	D=6	;DESTINATION OPERATOR
	MSR=7	;MACHINE STATUS REGISTER
	S=10	;SOURCE OPERATOR
	MB=11	;MEMORY BUFFER
	XR=12	;INDEX REGISTER
	R=16	;LINK REGISTER
	TMP=13	;TEMPORARIES
	TMP1=14
	TMP2=15
	P=17	;PUSHDOWN POINTER.

	DEFINE ACTIVE(NM,L)
<	SKIPE ,NM'UCB+UACT
	JRST L
	MOVE TMP,TM
	ADD TMP,NM'TIM
	MOVEM TMP,NM'UCB+UTIM
	MOVEI TMP,NM'UCB
	MOVEM TMP,.UCBAD
	PUSHJ P,.ACTVT>

	DEFINE CANCEL(NM)
<	SKIPN ,NM'UCB+UACT
	POPJ P,
	MOVEI TMP,NM'UCB
	MOVEM TMP,.UCBAD
	PJRST .CNCEL>

	NUMGR=6	;# OF GENERAL REGISTERS.

;SIMULATOR MACROS
;
	DEFINE INTR(DV)	;DEVICE INTERRUPT SET/CLEAR TOUTINES
<	EXTERN DV'LVL	;DEVICE LEVEL REGISTER IN PI ARRAY
	EXTERN DV'MLV	;PI LEVEL EXPRESSED AS A MASK
;
SET'DV:	MOVE TMP,[1B<DV'BIT>]	;GET DEVICE INTR REQUEST FLAG AND
	IORM TMP,DV'LVL	;SET IT IN DEVICE PI LEVEL.
STB'DV:	IORI F,DV'MLV	;SET LEVEL REQUEST FLAG IN F.
	POPJ P,
;
CLR'DV:	MOVE TMP,[1B<DV'BIT>]	;GET DEVICE INTR REQUEST FLAG AND
	ANDCAB TMP,DV'LVL	;CLEAR IT IN DEVICE PI LEVEL.
	JUMPN TMP,STB'DV
	TRZ F,DV'MLV	;CLEAR LEVEL REQUEST FLAG IN F.
	POPJ P,
>

	DEFINE RSTOFF(DV)	;CLEAR DEVICE FLAG DURING RESET
<	MOVE F,STM+F
	PUSHJ P,CLR'DV
	MOVEM F,STM+F
>

	DEFINE RSTON(DV)
<	MOVE F,STM+F
	PUSHJ P,SET'DV
	MOVEM F,STM+F
>

	DEFINE DO(NUM,MAC)	;REPEAT MACRO 'NUM' TIMES
<I==1
	REPEAT NUM,<
	MAC(\I)
	I==I+1>
>

	EXTERN SMGR
	LIST
