	TITLE	EDITOR - MIMIC TTY CONTROLLER
	HISEG
	SUBTTL	INTRODUCTION
;
;
;
;THE EDITOR IS RESPONSIBLE FOR ACQUISITION OF USER INPUT AND
;CONVERSION OF SAME TO MIMIC SIXBIT STRINGS, AND FOR THE
;TYPING OUT OF MIMIC SIXBIT AND SEVENBIT STRINGS.  MIMIC SIXBIT
;DIFFERS FROM NORMAL '10 SIXBIT IN THE FOLLOWING RESPECTS:  STRINGS
;ARE TERMINATED WITH A ZERO BYTE; AND THE FOLLOWING CHARACTER
;SUBSTITUTIONS HAVE BEEN MADE:
;	CHARACTER	REPLACES	CHARACTER
;	  00				  SPACE
;	  SPACE				  QUOTE
;	  RETURN			  NUMBER
;	  LNFEED			  DOLLAR
;	  ALTMOD			  PERCNT
;	  TAB				  AMPSND
;
;
;
	INTERN	REDTXT
	INTERN	WRTTXT
	EXTERN MIMREN
	EXTERN DMPCHR
	EXTERN SMGR
;
;	AC ASSIGNMENTS
;
	BFR=1
	TRM=2
	BCN=3
	CHR=4
	ZERO=5
	TA1=12
	STK=17

	SUBTTL  TEXT INPUT ROUTINE
;EDITOR, PAGE 2
;
RETRY:	OUTSTR [BYTE (7)136,125,15,12(8)0]
	JRST .+2	;ON ^U, OUTPUT ^,U,CR,LF, LIKE THE MONITOR.
REDTXT:	OUTCHR [ABLINE]	;BEGINNING OF INPUT:  OUTPUT CHAR.
	HRRZ BFR,TXTADR	;GET THE INPUT BUFFER ADDRESS.
	HRLI BFR,440600	;MAKE IT INTO A 6-BIT BYTE PTR.
	SETZB BCN,@TXTADR	;ZERO THE BUFFER AND THE BUFFER COUNT.
	TDZA ZERO,ZERO		;ZERO THE ZERO REGISTER AND SKIP.
;
DOBELL:	OUTCHR [AILLEG]	;ILLEGAL CHAR...MAKE NASTY REMARK.
GETTTY:	INCHRW CHR	;GET THE NEXT INPUT CHARACTER.
;
CHRCMP:	CAIN CHR,AWRU	;CHAR= CANCEL COMMAND?
	JRST CCNCEL	;YES, GO SIMULATE ^C ^C REENTER.
	CAIN CHR,AKLINE	;CHAR= KILL LINE?
	JRST RETRY	;YES, GO START OVER.
	CAIN CHR,ARBOUT	;CHAR= RUB OUT CHARACTER?
	JRST RUBOUT	;YES.
	CAIG BCN,BUFMAX	;NO, BUFFER FULL?
	PUSHJ STK,SEVTO6	;NO, CONVERT CHAR TO MIMIC SIXBIT.
	JRST DOBELL	;ILLEGAL OR NO ROOM... GO COMPLAIN.
	IDPB CHR,BFR	;STORE CHAR IN BUFFER.
	AOJ BCN,	;INCREMENT THE BUFFER COUNT.
	HRRZ TRM,TXTTRM	;GET ADDRESS OF TERMINATOR TABLE.
TERMLP:	MOVE TA1,@TRM	;GET FIRST (NEXT) TERMINATOR.
	JUMPE TA1,GETTTY	;END OF TABLE? YES, NO TERMINATOR.
	CAME TA1,CHR	;NO, CHR= TERMINATOR.
	AOJA TRM,TERMLP	;NO MATCH, CONTINUE SEARCH.
	IDPB ZERO,BFR	;MATCH.... ZERO END OF BUFFER AND EXIT.
WRTXIT:	POPJ STK,
;
RUBOUT:	JUMPE BCN,REDTXT	;BUFFER EMPTY?
	CAMGE BFR,[360000000001]	;NO.  POINTING AT LEFTMOST BYTE?
	ADD BFR,[060000000000+360000000001]	;NO, DECREMENT PTR.
	SUB BFR,[360000000001]	;YES, RESET PTR TO RIGHT-BYTE AND GO
		;BACK TO PREVIOUS WORD.
	OUTCHR [ANONO]	;TYPE OUT GRONK CHARACTER.
	SOJA BCN,GETTTY	;DECREMENT BUFFER COUNT, GO AWAY.
;
CCNCEL:	OUTSTR CNCMSG	;CANCEL... TYPE OUT CANCELLED MESSAGE.
	JRST MIMREN	;GO DO REENTER.
CNCMSG:	IFE LEVELD,<	ASCIZ /^E
CANCELLED
/>
	IFN LEVELD,<	ASCIZ /
CANCELLED
/>	;LEVEL-D MONITOR ECHOES ^E AUTOMATICALLY.
;EDITOR, PAGE 3
;
WRTTXT:	HRRZ BFR,TXTADR	;GET OUTPUT TEXT ADDRESS AND CONVERT IT
	HRLI BFR,440600	;INTO A 6-BIT BYTE POINTER.
WRTTXL:	ILDB CHR,BFR	;GET NEXT CHARACTER.
	JUMPE CHR,WRTXIT	;IF ZERO, DONE.
	PUSHJ STK,SIXTO7	;CONVERT IT TO 7-BIT.
		;CALL ON DMPCHR CHECKS DUMP MODE.
	PUSHJ STK,DMPCHR	;YES, DUMP TO FILE.  NO,
	OUTCHR CHR	;OUTPUT IT.
	JRST WRTTXL
;EDITOR, PAGE 4
;
;
;
;
SEVTO6:	;			;TEST FOR CODES TO BE REPLACED.
	CAIGE	CHR,042		;REPLACEABLE CODE?
	JRST	REPLAC		;NO.
	CAIGE	CHR,046		;PERHAPS; REPLACEABLE?
	JRST	ILLEGL		;YES, TREAT AS ILLEGAL.
REPLAC:	CAIN	CHR,011		;VALID; IS IT TAB?
	HRRZI	CHR,046		;YES, REPLACE WITH CODE FOR AMPERSAND
	CAIN	CHR,012		;LINE-FEED?
	HRRZI	CHR,044		;USE DOLLAR.
	CAIN	CHR,015		;CARRIAGE-RETURN?
	HRRZI	CHR,043		;NUMBER.
	CAIN	CHR,040		;SPACE?
	HRRZI	CHR,042		;QUOTE.
	CAIN	CHR,175		;ALTMODE?
	HRRZI	CHR,045		;PERCENT.
	CAIGE	CHR,141		;LOWER-CASE CHARACTER?
	JRST	.+3		;NO.
	CAIGE	CHR,173		;PERHAPS.
	TRZ	CHR,040		;DEFINITELY; TRANSLATE TO UPPER CASE.
;AT THIS POINT, VALID CHARACTERS LIE IN THE RANGE 000,
	;040-137.  ALL OTHERS ARE ILLEGAL AND WILL CAUSE FALL-THROUGH.
	CAIL	CHR,040	;NO, .GE. 40?
	CAILE	CHR,137		;YES, BUT .LE. 137?
	JRST	ILLEGL		;NO, ILLEGAL.
LEGAL:	ADDI	CHR,040		;YES, CONVERT TO SIXBIT.
	ANDI	CHR,077		;NOTE THAT RUBOUT STILL MAPS TO ZEROO.
	AOS	0(STK)		;FORCE A SKIP RETURN.
ILLEGL:	POPJ	STK,		;EXIT.
	SUBTTL	SIXBIT TO SEVENBIT MAPPER
;
;
;
SIXTO7:	ADDI	CHR,040		;TRANSLATE TO PARITY-LESS ASCII.
	CAIG	CHR,046		;IS THE CHARACTER SPECIAL?
	HRRZ	CHR,SPCTBL-40(CHR) ;YES, DERIVE THE CODE WE WANT.
	POPJ	STK,		;EXIT.
;
SPCTBL:	EXP	000		;TERMINATOR.
	EXP	041		;EXCLAMATION MARK.
	EXP	040		;SPACE.
	EXP	015		;CARRIAGE-RETURN.
	EXP	012		;LINE-FEED
	EXP	175		;ALTMODE.
	EXP	011		;TAB.
;
	END
