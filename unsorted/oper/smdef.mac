	XLIST
	SALL
;
;SM CODE IS "ASSEMBLED" BY MACROS DEFINED FOR EACH INSTRUCTION
;
;THESE TWO MACROS DEFINE THE FIELDS OF THE 2 BASIC
;SM INSTRUCTION FORMATS
;
;
DEFINE	SFORM1(OP,MOD,SKP,NDX,BAS)
	<
	BYTE(1)0(5)OP(5)MOD(3)SKP(4)NDX(18)BAS
	>
;
DEFINE	SFORM2(OP,MOD,REG,BAS)
	<
	BYTE(1)0(5)OP(5)MOD(1)0(6)REG(18)BAS
	>
;
;
;THESE MACROS DEFINE THE MACROS WHICH "ASSEMBLE" EACH INSTRUCTION
;
;GROUP 1, INSTRUCTIONS WITH NO OPERANDS WHATSOEVER
;
DEFINE	SMGRP1(OPC)
	<
	DEFINE	OPC
		<
		SFORM1(OPC,0,0,0,0)
		>
	>
;
;GROUP 2, INSTRUCTIONS TAKING ONLY N MODIFICATION
;
DEFINE	SMGRP2(OPC)
	<
	DEFINE	OPC
		<
		SFORM1(OPC,0,0,0,0)
		>
	DEFINE	OPC'N
		<
		SFORM1(OPC,0,2,0,0)
		>
	>
;
;GROUP 3, INSTRUCTIONS HAVING NO OPERAND OTHER THAN AN ADDRESS
;
DEFINE	SMGRP3(OPC)
	<
	DEFINE	OPC(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,0,X'INDEX,BASE)
		>
	>
;
;
;
;GROUP 4, INSTRUCTIONS WITH R, N, AND F VARIANTS
;
DEFINE	SMGRP4(OPC)
	<
	DEFINE	OPC(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,0,X'INDEX,BASE)
		>
	DEFINE	OPC'P(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,2,X'INDEX,BASE)
		>
	DEFINE	OPC'N(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,1,X'INDEX,BASE)
		>
	DEFINE	OPC'A(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,3,X'INDEX,BASE)
		>
	DEFINE	OPC'R(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,4,X'INDEX,BASE)
		>
	DEFINE	OPC'F(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,5,X'INDEX,BASE)
		>
	DEFINE	OPC'RF(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,6,X'INDEX,BASE)
		>
	DEFINE	OPC'AF(BASE,MODE,INDEX)
		<
		SFORM1(OPC,MODE,7,X'INDEX,BASE)
		>
	>
;
;
;GROUP 5, INSTRUCTIONS USING GENERAL REGISTERS
;
;FORMAT DEPENDS ON ADDRESSING MODE
;IF GR FIELD IS >77, FIELD IS ASSUMED TO BE SYMBOLIC, AND GR
;ADDRESS IS COMPUTED BY SUBTRACTING GR TABLE BASE
;
DEFINE	SMGRP5(OPC)
	<
	DEFINE	OPC(GR,BASE,MODE,INDEX)
		<
		IFG MODE-NOINDX,
			<SFORM1(OPC,MODE,X'GR,X'INDEX,BASE)
			>
		IFLE MODE-NOINDX,
			<SFORM2(OPC,MODE,X'GR,BASE)
			>
		>
	>
;
	DEFINE RSM
<	PUSHJ STK,SMRSME>
	EXTERN SMRSME
;
;DEFINITION OF SM OP CODES
;
NOP=0
RSS=1
RSR=2
;
JMP=7
XEQ=10
CSS=11
MCH=12
ASM=13
XHM=14
CHM=15
JHM=16
SMZ=17
ABS=20
;
LDG=30
STG=31
ADG=32
SGS=33
SGD=34
JGN=35
JGZ=36
;
;DEFINITIONS OF SM'S ADDRESSING MODES
;
I=0
D=1
R=2
L=3
RR=4
RL=5
LR=6
LL=7
;
NOINDX=17
;
XI=20
XD=21
RP=22
LP=23
RX=24
LX=25
XR=26
XL=27
RPR=30
RPL=31
LPR=32
LPL=33
RXR=34
RXL=35
LXR=36
LXL=37
;
	X=0	;MACRO BUG
;
;DEFINITION OF SM'S INSTRUCTIONS
;
;GROUP 1, INSTRUCTIONS WITH NO OPERANDS WHATSOEVER
;
	SMGRP1	NOP
;
;GROUP 2, INSTRUCTIONS WITH N VARIANT ONLY
;
	SMGRP2	RSS
	SMGRP2	RSR
;
;GROUP 3, INSTRUCTIONS WITH ONLY AN ADDRESS
;
	SMGRP3	JMP
	SMGRP3	XEQ
	SMGRP3	JHM
;
;GROUP 4, INSTRUCTIONS WITH R N, AND F VARIANTS
;
	SMGRP4	CSS
	SMGRP4	MCH
	SMGRP4	ASM
	SMGRP4	ABS
	SMGRP4	XHM
	SMGRP4	CHM
	SMGRP4	SMZ
;
;GROUP 5, INSTRUCTIONS USING GR'S
;
	SMGRP5	LDG
	SMGRP5	STG
	SMGRP5	ADG
	SMGRP5	SGS
	SMGRP5	SGD
	SMGRP5	JGN
	SMGRP5	JGZ
;
;
;
	LIST
