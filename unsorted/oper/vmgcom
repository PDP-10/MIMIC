	SALL
	TITLE VMGCOM	GRI-909 COMMUNICATIONS REGION
	ENTRY CDMLST
	INTERN SRCTBL,DSTTBL,SFTBL,INTTBL
	EXTERN STMSR,TRP,ISR,SWR,SFM
	EXTERN FOM,FOINT,FOBRK,ILLOPS,ILLOPD
	EXTERN MFETCH,MSELF,CPUDDM,STORSC,GETSC
IFN M99,<	EXTERN GRIXR>
IFN AO,<	EXTERN AX,AY,AOX,SFATBL,STAX,STAY,FOA,RDAO>
IFN EAO,<	EXTERN FOEAO>
IFN BSW,<	EXTERN BSWR,SWAPR>
IFN BPK,<	EXTERN BPKR,PACKR>
IFN GR,<	EXTERN GPR>
IFN RTC,<	EXTERN SFRTC,FORTC,RTCDDM>
IFN PTR+PTP,<	EXTERN PTRUCB,PTPWR,PTRDDM,PTPDDM
	EXTERN SFPTBL,FOPT,PTRNX>
IFN TTI+TTO,<	EXTERN TTIUCB,TTOWR,TTIDDM,TTODDM,SFTTBL,FOTT,LSRDDM>
IFN DSK,<	EXTERN DSKDDM,DSK70R,DSK70W,DSKSLCT,SFDSK
	EXTERN DSKWCT,DSKCAR>
IFN MUX,<	EXTERN MUXCHL>
IFN ADC,<	EXTERN ADCDDM,ADCBUF,FOADC,SFADCT>
IFN DAC,<	EXTERN DACDDM,DACUCB,DACWR>
;
CDMLST:	Z ,CPUDDM	;CPU IS FIXED.
IFN PTR,<	Z ,PTRDDM>
IFN PTP,<	Z ,PTPDDM>
IFN TTI,<	Z ,TTIDDM>
IFN LSR,<	Z ,LSRDDM>
IFN TTO,<	Z ,TTODDM>
IFN RTC,<	Z ,RTCDDM>
IFN DSK,<	Z ,DSKDDM>
IFN ADC,<	Z ,ADCDDM>
IFN DAC,<	Z ,DACDDM>
	Z



	IOP=PUSHJ P,@ILLPTR	;ILLEGAL OPERATOR
	NOOP=JFCL 0,0	;NULL OPERATOR

ILLPTR:	ILLOPS	;DUE TO LOADER FUCK-UP.
GFPTR:	GFETCH
IL1PTR:	ILLOPD
IFN MUX,<MUXPTR:	POINT 5,MUXCHL,35>
;
;GRI-909 COMMUNICATIONS REGION, PAGE 2
;CONDITIONAL EXECUTE TABLES.
;SRCTBL:	FETCH OR JUMP TO SOURCE OPERATOR
;DSTTBL:	STORE OR JUMP TO DESTINATION OPERATOR
;SFTBL:		SENSE FUNCTION TABLE
;FOTBL:		FUNCTION OUT TABLE.
;
SRCTBL:	CLEAR BR,	;0= NULL
	HRRZ BR,IR	;1= IR
	XCT ,FOTBL(D)	;2= FUNCTION GENERATOR
	HRRZ BR,TRP	;3= TRP
	HRRZ BR,ISR	;4= ISR
	HRRZ BR,MA	;5= MA
	JRST MFETCH	;6= MEMORY
	JRST GETSC	;7= SC
	HRRZ BR,SWR	;10= SWR
	REPEAT 6,<	IOP>	;11-16 ARE OPTIONS
	HRRZ BR,MSR	;17= MSR
	REPEAT 60,<	IOP>	;20-77 ARE OPTIONS

	IOP=PUSHJ P,@IL1PTR
;
DSTTBL:	NOOP	;0-1 = NULL
	NOOP
	HALT	;2= SF (SHOULD NOT GET HERE!)
	HRRZM BR,TRP	;3= TRP (IF S=06)
	HRRZM BR,ISR	;4= ISR
	NOOP	;5= MA
	JRST MSELF	;6= MEMORY
	JRST STORSC	;7= SC
	NOOP	;10= SWR
	REPEAT 6,<	IOP>	;11-16 ARE OPTIONS
	JRST STMSR	;17= MSR
	REPEAT 60,<	IOP>	;20-77 ARE OPTIONS
;
;GRI-909 COMMUNICATIONS REGION, PAGE 3
;
SFTBL:	JRST SFM	;0= MACHINE
	REPEAT 10,<	NOOP>	;1-10 CANNOT BE SENSED.
	REPEAT 6,<	IOP>	;11-16 ARE OPTIONS
	NOOP	;17= MSR
	REPEAT 60,<	IOP>	;20-77 ARE OPTIONS
;
	EXTERN GFETCH
	NOF=AOJA SC,@GFPTR
FOTBL:	AOJA SC,FOM	;0= MACHINE, INCREMENT SC FIRST
	AOJA SC,FOBRK	;1= BKPT, INCREMENT SC FIRST
	NOF
	NOF	;2-3
	JRST FOINT	;4= INTERRUPTS
	REPEAT 73,<	NOF>	;5-77 ARE OPTIONS

INTTBL:	REPEAT INTSIZ,<	Z>	;INTERRUPTS TABLE
;
;GRI-909 COMMUNICATIONS REGION, PAGE 4
;OPTIONS AND DEVICES, CONDITIONAL ASSEMBLY

	DEFINE RDT(NM,II)
<IFG NM,<	RELOC SRCTBL+NM'A
	II>>

	DEFINE WRT(NM,II)
<IFG NM,<	RELOC DSTTBL+NM'A
	II>>

	DEFINE INT(NM)
<IFG NM,<	RELOC INTTBL+INTSIZ-1-NM'B
	NM'I>>

	DEFINE SFT(NM,NME,II)
<IFG NM,<	RELOC SFTBL+NME'A
	II>>

	DEFINE FOT(NM,NME,II)
<IFG NM,<	RELOC FOTBL+NME'A
	II>>

;BYTE SWAPPER AND PACKER:

	RDT BSW,<	HRRZ BR,BSWR>
	RDT BPK,<	HRRZ BR,BPKR>
	WRT BSW,<	JRST SWAPR>
	WRT BPK,<	JRST PACKR>

;ARITHMETIC OPERATOR:

	RDT AO,<	HRRZ BR,AX
	HRRZ BR,AY
	JRST RDAO>
	WRT AO,<	JRST STAX
	JRST STAY
	NOOP>
	SFT AO,2+AO,<	XCT SFATBL(XR)>
	FOT AO,2+AO,<	JRST FOA>

;EXTENDED ARITHMETIC OPERATOR:

	FOT EAO,EAO,<	JRST FOEAO>

;GENERAL REGISTERS:

	RDT GR,<	HRRZ BR,GPR+0
	HRRZ BR,GPR+1
	HRRZ BR,GPR+2
	HRRZ BR,GPR+3
	HRRZ BR,GPR+4
	HRRZ BR,GPR+5>

	WRT GR,<	HRRZM BR,GPR+0
	HRRZM BR,GPR+1
	HRRZM BR,GPR+2
	HRRZM BR,GPR+3
	HRRZM BR,GPR+4
	HRRZM BR,GPR+5>

;MODEL 99 EXTRA FEATURES

	RDT M99,<	HRRZ BR,GRIXR
	HRRZ BR,TRP>
	WRT M99,<	HRRZM BR,GRIXR
	HRRZM BR,TRP>
;
;GRI-909 COMMUNICATIONS REGION, PAGE 5
;DEVICES AND OPTIONS, CONTINUED

;TTI/TTO:

	RDT TTI,<	HRRZ BR,TTIUCB+UBUF>
	WRT TTO,<	PUSHJ P,TTOWR>
	INT TTI
	INT TTO
	FOT TTI+TTO,TTI,<	AOJA SC,FOTT>
	SFT TTI+TTO,TTI,<	XCT SFTTBL(XR)>

;HSR/HSP:

	RDT HSR,<	HRRZ BR,PTRUCB+UBUF>
	WRT HSP,<	PUSHJ P,PTPWR>
	INT HSR
	INT HSP
	SFT HSR+HSP,HSR,<	XCT SFPTBL(XR)>
	FOT HSP-HSR,HSP,<	AOJA SC,PTRNX>
	FOT HSR,HSR,<	AOJA SC,FOPT>

;RTC:

	SFT RTC,RTC,<	JSP R,SFRTC>
	FOT RTC,RTC,<	AOJA SC,FORTC>
	INT RTC

;DSK:

	RDT DSK,<	HRRZ BR,DSKWCT
	HRRZ BR,DSKCAR
	JSP R,DSK70R>

	WRT DSK,<	HRRZM BR,DSKWCT
	HRRZM BR,DSKCAR
	JSP R,DSK70W>

	INT DSK
	SFT DSK,2+DSK,<	JSP R,SFDSK>
	FOT DSK,2+DSK,<	HRRZM IR,DSKSLCT>

;ADC/MUX/DAC:

	RDT MUX,<	HRRZ BR,MUXCHL>
	WRT MUX,<	DPB BR,MUXPTR>
	RDT DAC,<	HRRZ BR,DACUCB+UBUF>
	WRT DAC,<	PUSHJ P,DACWR>
	INT ADC
	RDT ADC,<	HRRZ BR,ADCBUF>
	WRT ADC,<	NOOP>
	SFT ADC,ADC,<	XCT SFADCT(XR)>
	FOT ADC,ADC,<	AOJA SC,FOADC>

	END
