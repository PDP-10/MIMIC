	TITLE VMSERV  VM UTILITY ROUTINES
	HISEG
;
;VMSERV IS NOT PROPERLY PART OF MIMIC.  INSTEAD,  TI CONSISTS OF
;UTILITY SUBROUTINES WHICH HAVE BEEN USED IN MOST VM'S TO DATE.
;CNFOUT:	DUMP BRKBLK ON BREAK OR TRAP
;RXCPT:	DIAGNOSE READ FAILURE
;PXCPT:		DIAGNOSE OUTPUT FAILURE
;
	INTERN SLPCHK,CNFOUT,RXCPT,PXCPT
	EXTERN SYSLSP,WRTNAM,WRTNOL,SMGR,INTSTP
	EXTERN .ACTVQ,.EVNTM,STKSAV

	AC1=10
	AC2=11
	STK=17

IFN DTALIN,<OPDEF HIBER[CALLI -1]>
IFN ONLINE,<OPDEF HIBER[CALLI -42]>
;VMSERV, PAGE 2
;
CNFOUT:	OUTSTR [BYTE (7)15,12(22)0]	;OUTPUT INITIAL CR, LF.
	OUTSTR @CMSGAD	;OUTPUT MESSAGE.
	HRRZ AC1,BRKBLK+BUCB	;GET THE INVOLVED UNIT.
	MOVE AC2,UNAM(AC1)	;GET THE UNIT'S NAME.
	MOVEM AC2,NAME	;SAVE THE NAME.
	HRRZ AC2,UDDM(AC1)	;GET THE PARENT DDM.
	MOVE AC2,DADR(AC2)	;GET THE ADDRESS RADIX+BITS.
	HLRZM AC2,NRADIX	;STORE RADIX+BITS.
	HRRZM AC2,NBITS
	MOVE AC2,BRKBLK+BFRM	;GET THE FRAME#.
	MOVEM AC2,VALUE	;SAVE IT.
	PUSHJ STK,WRTNAM	;TYPE THE UNIT NAME,
	OUTCHR [40]	;SPACE,
	PUSHJ STK,WRTNOL	;FRAME#,
	OUTSTR [BYTE (7)15,12(22)0]	;CR, LF.
	POPJ STK,	;EXIT.
;
;VMSERV, PAGE 3
;
RXCPT:	HRRZ AC1,.UCBAD	;GET THE INVOLVED UNIT.
	HLRZ AC2,USTAT(AC1)	;GET ITS STATUS.
	TRNN AC2,EFRM	;FRAME# ERROR?
	JRST PCOMN	;NO.
	MOVE AC2,UFLGS(AC1)	;YES, GET THE FLAG BITS.
	TRNE AC2,1	;BREAK ON EOF FLAG SET?
	POPJ STK,	;YES.
PXCPT:	HRRZ AC1,.UCBAD	;NO, GET THE INVOLVED UNIT.
	HLRZ AC2,USTAT(AC1)	;GET ITS STATUS.
PCOMN:	ANDI AC2,7	;MASK OFF ALL EXCEPT ERROR BITS.
	JFFO AC2,.+2	;ANY ERROR?
	POPJ STK,	;NO.
	MOVE AC2,MTBL-^D33(AC2+1)	;GET THE PROPER MESSAGE ADDRESS.
	MOVEM AC2,CMSGAD
	MOVE AC2,UFRM(AC1)	;GET THE FRAME# AND SAVE IN BRKBLK.
	MOVEM AC2,BRKBLK+BFRM
	HRRZM AC1,BRKBLK+BUCB	;SAVE THE UNIT ADDRESS IN BRKBLK.
	SETZM ,BRKBLK+BACT
	PJRST INTSTP	;DO TRAP, THEN EXIT.
;
MTBL:	MUNK	;BIT 33=I/O ERROR
	MUNAT	;BIT 34=UNATTACHED UCB
	MFRM	;BIT 35=FRAME # ERROR
;
MUNK:	ASCIZ +I/O ERROR AT +
MUNAT:	ASCIZ /UNATTACHED UNIT AT /
MFRM:	ASCIZ /FRAME# ERROR AT /
;
;
;VMSERV, PAGE 4
;
	AC=AC2
SLPCHK:	HLLZ AC,BRKBLK+BBRK	;GET BREAK BITS, IF ANY.
	TLNN AC,004000	;SLEEP SET?  NO, DO NORMAL BREAK AT CNFOUT.
	JRST CNFOUT
	SKIPG AC,.ACTVQ	;SLEEP... GET ADDRESS OF 1ST ACTIVE UCB.
	JRST TOSIML	;QUEUE EMPTY, DO NOT SLEEP.
IFN LEVELD+ONLINE+DTALIN+TYMSHR,<
	SKIPLE ,UACT(AC)	;IS 1ST UCB ALSO THE LAST?
	JRST NOTTY	;NO, DOENT PLAY TTY GAMES.
	MOVSI AC,000010	;SET "WAIT FOR CHARACTER" FLAG
	HIBER AC,	;AND GO HIBERNATE.  WHEN WINTERS OVER,
	JFCL		;OR EVEN IF WINTER NEVER OCCURRED:
>
NOTTY:	MOVE AC,SYSLSP	;IN ANY CASE, SET CURRENT TIME
	MOVE AC1,.EVNTM	;EQUAL TO TIME OF FIRST CHEDULED EVENT.
	MOVEM AC1,@SYTIM(AC)
TOSIML:	MOVE AC,SYSLSP	;GET ADDRESS OF BLDUP ROUTINE FROM
	PUSHJ STK,@SYBLD(AC)	;SYSTEM LIST AND CALL IT.
	MOVE STK,STKSAV	;RESTORE PUSHDOWN LIST AND
	POPJ STK,	;RETURN TO VM.  NOTE THAT THE TTY MODES HAVE
			;NOT BEEN TOUCHED.

	END
