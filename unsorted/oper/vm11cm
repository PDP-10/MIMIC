	SALL
	TITLE VM11CM	PDP-11 COMMUNICATIONS REGION
	INTERN CDMLST,PERBNK,ADRTBL,RDTBL,WRTBL,HMASK,HADD
	INTERN BR7TBL,BR6TBL,BR4TBL,BR5TBL,WRBTBL
	EXTERN PDP11S
;
;THE COMMUNICATIONS REGION CONSISTS OF SEVERAL CONDITIONAL TABLES:
;1) CDMLST:	LIST OF ALL CONFIGURED CONTROLLERS
;2) ADRTBL:	LIST OF ALL LEGAL DEVICE ADDRESSES (HASHED)
;3) PERBNK:	CONTENTS OF DEVICE ADDRESSES
;4) RDTBL:	READ INSTRUCTIONS FOR DEVICES
;5) WRTBL:	WRITE INSTRUCTIONS FOR DEVICES
;6) WRBTBL:	WRITE BYTE INSTRUCTIONS FOR DEVICES
;7)-10) BR7TBL,...,BR4TBL:	INTERRUPT VECTOR TABLES
;
;A NOTE ON MODEL NUMBERS.  THE 11/05, 11/20, 11/25, AND 11/45
;ARE REPRESENTED BY MODEL NUMBERS OF 5, 20, 25, AND 45 RESPECTIVELY.
;THE 11/20 WITH KS-11 IS ASSIGNED AN INTERNAL MODEL NUMBER OF
;44, SIGNIFYING THAT, LIKE THE 45, IT HAS AN 18 BIT UNIBUS RATHER
;THAN A 16 BIT BUS.
;
	DEFINE DFT(F,S)	;DEFINE DEFAULT STATE OF FLAG
<IFNDEF F,<	F=S>>

	DEFINE CD(F,S)	;CONDITIONAL DDM ENTRY
<IFN F,<	EXTERN S
	S>>

;DEFINE DEFAULT STATE OF CONDITIONAL ASSEMBLY FLAGS:

	DFT PTR,0	;ALL DEVICES ARE OUT EXCEPT CPU, 1ST KL.
	DFT PTP,0
	DFT CLK,0
	DFT PK,0
	DFT RF,0
	DFT RK,0
	DFT RP,0
	DFT RC,0
	DFT CPU,^D8
	DFT MODEL,^D20
	DFT DT,0
	DFT DM,0
	DFT KL,1
IFGE KL-1,<
	TTI==	1	;CONDITIONAL FLAGS FOR 1ST KL
	TTO==	1
>
	DFT KBN,0
	DFT MBR,0
	DFT AD,0

	UDV=KBN
	XMA=12	;EXTERNAL PAGE ADDRESS
IFNDEF DMVCTR,<	DMVCTR=310>
IFNDEF KLVCTR,<	KLVCTR=300>	;FLOATING VECTOR ARITHMETIC
IFG <KL-1>,<	DMVCTR=10*<KL-1>+KLVCTR>
;
;PDP-11 COMMUNICATIONS REGION, PAGE 2.
;CDM LIST.
;
	DEFINE CKLD(N)	;DEFINE KL-11 DDM LINKS
<IFGE KL-^D'N,<	EXTERN TTI'N'DDM,TTO'N'DDM
	Z ,TTI'N'DDM
	Z ,TTO'N'DDM
>
>

CDMLST:	CD CPU,CPUDDM	;CPU, 1ST KL ARE STANDARD.
	CD TTI,TTIDDM
	CD TTO,TTODDM
	CD PTR,PTRDDM
	CD PTP,PTPDDM
	CD CLK,CLKDDM
	CD PK,PKDDM
	CD RF,RFDDM
	CD RP,RPDDM
	CD RC,RCDDM
	CD RK,RKDDM
	CD DT,DTDDM
	CD DM,DMDDM
	CKLD 2;	;TELETYPES 2-8
	CKLD 3
	CKLD 4
	CKLD 5
	CKLD 6
	CKLD 7
	CKLD 8
	CD KBN,KBNDDM
	CD MBR,MBRDDM
	CD AD,ADDDM
	Z
;
;VECTOR TABLES.
;
BR7TBL:	REPEAT PV7,<	Z>
BR6TBL:	REPEAT PV6,<	Z>
BR5TBL:	REPEAT PV5,<	Z>
BR4TBL:	REPEAT PV4,<	Z>

	DEFINE CV(NM,L,V)	;DEFINE VECTOR TABLE ENTRY.
<	RELOC BR'L'TBL+NM'PV
IFN MODEL-^D44,<	XWD 0,<V>/2>
IFE MODEL-^D44,<	XWD 0,<V>>
>
;
;WE'LL DO THESE LAST, SINCE THEY DESTROY THE LOCATION COUNTER.
;
;
;PDP-11 COMMUNICATIONS REGION, PAGE 3
;DEVICE ADDRESS TABLES.  THE DEVICE TABLES ARE HASH-CODED,
;AND WE HAVE SOME FAIRLY HAIRY MACROS TO DO THE ENCODEMENT.
;THE SECRET OF THE PROCESS LIES IN THE "PROBE RECORD VARIABLES"
;PRB0,...,PRB'N', WHICH FOR EACH TABLE LOCATION  RECORDS
;WHETHER THE LOCATION IS ALREADY IN USE.

	DEFINE CLRP(I)	;CLEAR PROBE I
<	PRB'I=0>

	DEFINE SETP(I)	;SET PROBE I
<	PRB'I=1>

	DEFINE TSTP(I)	;TEST PROBE I
<	FLAG=PRB'I>

;AS THE FIRST STEP, WE CLEAR ALL THE PROBE VARIABLES:

	HSIZE=^D128	;TABLE SIZE
	HCONT=^D27	;OFFSET CONSTANT

	FLAG=0
	REPEAT HSIZE,<	CLRP(\FLAG)
	FLAG=FLAG+1>

;NOW DEFINE THE TABLES THEMSELVES, ALL INITIALLY EMPTY:

HMASK:	HSIZE-1	;TABLE SIZE MUST BE A POWER OF 2
HADD:	HCONT
ADRTBL:	REPEAT HSIZE,<	Z>	;ADDRESS TABLE
PERBNK:	REPEAT HSIZE,<	Z>	;CONTENTS
RDTBL:	REPEAT HSIZE,<	Z>	;READ INSTRUCTION
WRTBL:	REPEAT HSIZE,<	Z>	;WRITE INSTRUCTION
WRBTBL:	REPEAT HSIZE,<	Z>	;WRITE BYTE INSTRUCTION


;PDP-11 COMMUNICATIONS REGION, PAGE 3A
;SUPPLEMENTARY BUS ROUTINES.
IFN RF,<
PTCMA:	ANDI MB,177776
	DPB MB,CMAPTR
	POPJ P,
CMAPTR:	POINT 16,RFCMA,35
PTDAR:	POINT 16,RFUCB+UFRM,35
>	;MUST GO HERE BECAUSE SUBSEQUENT CODE DESTROYS
	;ASSEMBLER ORIGIN.
	INTERN BSEVAL
BSEVAL:	Z

;THE FOLLOWING PSEUDO-OPERATIONS ARE FOR USE WITH THE DATOB
;(WRBTBL) LINE OF THE SIMULATED UNIBUS

NOP=JFCL 0,0	;NO STORE
ODDNOP=PUSHJ P,IGNODD	;IGNORE DATOB TO ODD BYTE
BNRML=PUSHJ P,BSNRML	;STORE BYTE IN NORMAL FASHION

IGNODD:	JUMPGE MA,@WRTBL(XMA)	;IGNORE ODD BYTE.  IF EVEN BYTE,
		;BRANCH TO DATO PROCESSOR, WHICH IS ASSUMED TO
		;HAVE BEEN CALLED BY A PUSHJ.
	POPJ P,	;OTHERWISE, RETURN, NO STORE.

BSNRML:	JUMPGE MA,.+3	;A "NORMAL" REGISTER HAS ITS LOCATION IN
	DPB MB,ODDBYT	;PERBNK AND HENCE CAN
	POPJ P,		;BE STORED INTO DIRECTLY
	DPB MB,EVNBYT	;BY THIS ROUTINE.
	POPJ P,

ODDBYT:	POINT 8,PERBNK(XMA),27
EVNBYT:	POINT 8,PERBNK(XMA),35
;
;PDP-11 COMMUNICATIONS REGION, PAGE 4
;ACTUAL HASH ENCODEMENT MACROS:

	DEFINE HASH(I)	;HASH AN ADDRESS
<	LADDR=<I>!600000
	TORG=<<I>/2>&<HSIZE-1>
	PROBE TORG>

	DEFINE PROBE(I)
<	TSTP(\I)
IFE FLAG,<	SETP(\I)>
IFN FLAG,<	I=<I+HCONT>&<HSIZE-1>
	PROBE(I)>
>

	DEFINE DV(NM,CON,R,W,WB)
<	RELOC ADRTBL+TORG
	LADDR/2
	RELOC PERBNK+TORG
IFNDEF NM,<	INTERN NM
NM:>
	CON
	RELOC RDTBL+TORG
	R
	RELOC WRTBL+TORG
	W
	RELOC WRBTBL+TORG
IFB <WB>,<	CAIA>
IFNB <WB>,<	WB>
>


;ACTUAL DEVICE OVERLAYS, AT LAST!

IFN PTR,<	EXTERN PRBRD,PRSWR
	HASH 177550
	DV PRS,0,NOP,<PUSHJ P,PRSWR>
	HASH 177552
	DV PRB,0,<PUSHJ P,PRBRD>,NOP,NOP
>

IFN PTP,<	EXTERN PPSWR,PPBWR
	HASH 177554
	DV PPS,000200,NOP,<PUSHJ P,PPSWR>
	HASH 177556
	DV PPB,0,NOP,<PUSHJ P,PPBWR>,ODDNOP
>

IFN TTI,<	EXTERN TKS,TKSWR,TKBRD
	HASH 177560
	DV P,0,<HRRZ MB,TKS>,<PUSHJ P,TKSWR>
	HASH 177562
	DV TKB,0,<PUSHJ P,TKBRD>,NOP,NOP
>

IFN TTO,<	EXTERN TPS,TPSWR,TPBWR
	HASH 177564
	DV P,0,<HRRZ MB,TPS>,<PUSHJ P,TPSWR>
	HASH 177566
	DV TPB,0,NOP,<PUSHJ P,TPBWR>,ODDNOP
>

IFN CPU,<	EXTERN STPS
	HASH 177570
	DV SR,0,NOP,NOP,NOP
IFN MODEL-^D20,<
	HASH 177776
	DV PS,PS,<HRR MB,PS>,<PUSHJ P,STPS>
	>
IFE MODEL-^D20,<	EXTERN STPSD
	HASH 177776
	DV PS,PS,<HRR MB,PS>,<PUSHJ P,STPS>,<PUSHJ P,STPSD>
	>
>

IFE MODEL-^D44,<	EXTERN KSRWRD,KSRWWR	;KS-11 REGS
	HASH 177600
	DV KSS,0,NOP,NOP,NOP
	HASH 177602
	DV KSPL,0,NOP,<HRRM MB,KSPL>,BNRML
	HASH 177604
	DV KSRL,0,NOP,<HRRM MB,KSRL>,BNRML
	HASH 177606
	DV KSPH,0,NOP,<HRRM MB,KSPH>,BNRML
	HASH 177610
	DV KSRH,0,NOP,<HRRM MB,KSRH>,BNRML
	HASH 177612
	DV KSPC,0,NOP,<HRRM MB,KSPC>,BNRML
	HASH 177614
	DV KSAD,0,NOP,<HRRM MB,KSAD>,BNRML
	HASH 177616
	DV KSRW,0,<PUSHJ P,KSRWRD>,<PUSHJ P,KSRWWR>
>


IFN CLK,<	EXTERN LKSWR
	HASH 177546
	DV LKS,000200,NOP,<PUSHJ P,LKSWR>
>

IFN RF,<	EXTERN RFDAER, RFDAEW, RFADSR, RFSW, RFUCB
	HASH	177460
	DV	RFS,000200,<NOP>,<PUSHJ P,RFSW>
	HASH	177462
	DV RFWC,0,<NOP>,<HRRM MB,RFWC>,BNRML
	HASH	177464
	DV RFCMA,0,<ANDI MB,177777>,<PUSHJ P,PTCMA>
	HASH	177466
	DV RFDAR,0,<LDB MB,PTDAR>,<DPB MB,PTDAR>
	HASH	177470
	DV	RFDAE,0,<PUSHJ P,RFDAER>,<PUSHJ P,RFDAEW>
	HASH	177472
	DV RFDBR,0,<HRRZ MB,RFUCB+UBUF>,<HRRM MB,RFUCB+UBUF>
	HASH	177474
	DV	RFMAR,0,<NOP>,<NOP>,<NOP>
	HASH	177476
	DV	RFADS,0,<PUSHJ P,RFADSR>,<NOP>,<NOP>
>	;END OF RF DEFINITIONS

IFN RK,<EXTERN	RKDSRD,RKCSRD,RKCSWR,RKWCWR
	EXTERN	RKBAWR,RKDAWR,RKDBRD
	HASH	177400
	DV	RKDS,0,<PUSHJ P,RKDSRD>,NOP,NOP
	HASH	177402
	DV	RKER,0,NOP,NOP,NOP
	HASH	177404
	DV	RKCS,200,<PUSHJ P,RKCSRD>,<PUSHJ P,RKCSWR>
	HASH	177406
	DV	RKWC,0,NOP,<PUSHJ P,RKWCWR>
	HASH	177410
	DV	RKBA,0,NOP,<PUSHJ P,RKBAWR>
	HASH	177412
	DV	RKDA,0,NOP,<PUSHJ P,RKDAWR>
	HASH	177414
	DV	RKMR,0,NOP,NOP,NOP
	HASH	177416
	DV	RKDB,0,<PUSHJ P,RKDBRD>,NOP,NOP
>	;END OF RK DEFINITIONS.

IFN RP,<EXTERN RPDSRD,RPDSWR,RPCSRD,RPCSWR,RPCARD,RPCAWR
	EXTERN RPWCWR,RPDARD,RPDAWR
	HASH 176710
	DV RPDS,0,<PUSHJ P,RPDSRD>,<PUSHJ P,RPDSWR>
	HASH 176712
	DV RPER,0,NOP,NOP,NOP
	HASH 176714
	DV RPCS,200,<PUSHJ P,RPCSRD>,<PUSHJ P,RPCSWR>
	HASH 176716
	DV RPWC,0,NOP,<PUSHJ P,RPWCWR>
	HASH 176720
	DV RPBA,0,NOP,<HRRM MB,RPBA>,BNRML
	HASH 176722
	DV RPCA,0,<PUSHJ P,RPCARD>,<PUSHJ P,RPCAWR>
	HASH 176724
	DV RPDA,0,<PUSHJ P,RPDARD>,<PUSHJ P,RPDAWR>
	HASH 176726
	DV RPM1,0,NOP,NOP,NOP
	HASH 176730
	DV RPM2,0,NOP,NOP,NOP
	HASH 176732
	DV RPM3,0,NOP,NOP,NOP
	HASH 176734
	DV RPB1,0,NOP,NOP,NOP
	HASH 176736
	DV RPB2,0,NOP,NOP,NOP
	HASH 176740
	DV RPB3,0,NOP,NOP,NOP
>	;END OF RP DEFINITIONS

	DEFINE HSHK(N)	;EXTERNAL PAGE LINKS FOR TELETYPE N
<IFGE KL-^D'N,<	EXTERN TK'N'S,TP'N'S,TK'N'SWR
	EXTERN TP'N'SWR,TK'N'BRD,TP'N'BWR
	HASH <^D'N*10-20+176500>
	DV P,0,<HRRZ MB,TK'N'S>,<PUSHJ P,TK'N'SWR>
	HASH <^D'N*10-20+176502>
	DV TK'N'B,0,<PUSHJ P,TK'N'BRD>,NOP,NOP
	HASH <^D'N*10-20+176504>
	DV P,0,<HRRZ MB,TP'N'S>,<PUSHJ P,TP'N'SWR>
	HASH <^D'N*10-20+176506>
	DV TP'N'B,0,NOP,<PUSHJ P,TP'N'BWR>,ODDNOP
>
>

;NOW DO EXPANSIONS FOR TELETYPES 2-8

	HSHK 2
	HSHK 3
	HSHK 4
	HSHK 5
	HSHK 6
	HSHK 7
	HSHK 8



IFN UDV,<
	HASH 165000
	DV UDS,0,NOP,<HRRM MB,UDS>,BNRML
	HASH 165002
	DV UDB,0,NOP,<HRRM MB,UDB>,BNRML
>

IFN DM,<	EXTERN DMCSW,DMBARW,DMBCW,DMBAW
	HASH 175000
	DV DMCS,0,NOP,<PUSHJ P,DMCSW>
	HASH 175002
	DV DMBAR,0,NOP,<PUSHJ P,DMBARW>
	HASH 175004
	DV DMBC,0,NOP,<PUSHJ P,DMBCW>
	HASH 175006
	DV DMBA,0,NOP,<PUSHJ P,DMBAW>
>
IFN AD,<EXTERN ADCSWR,ADDBRD
	HASH 176770
	DV ADCS,0,NOP,<PUSHJ P,ADCSWR>
	HASH 176772
	INTERN ADDB		;DV WON'T DEFINE IT,
	ADDB=PERBNK+TORG	;BECAUSE IT'S ALSO AN OPCODE.
	DV ADDB,0,<PUSHJ P,ADDBRD>,NOP
>

IFN PK,<	EXTERN PKCSRW,PKCSBW
	HASH 172540
	DV PKCSR,0,NOP,<PUSHJ P,PKCSRW>
	HASH 172542
	DV PKCSB,0,NOP,<PUSHJ P,PKCSBW>
	HASH 172544
	DV PKCNTR,0,NOP,NOP,NOP
>
;
;PDP-11 COMMUNICATIONS REGION, PAGE 5
;
	CV PTR,4,70	;DEVICE VECTORS:  PTR=70
	CV PTP,4,74	;PTP=74
	CV TTI,4,60	;TTI=60
	CV TTO,4,64	;TTO=64
	CV CLK,6,100	;CLK=100
	CV PK,6,104	;KW11-P=104
	CV RK,5,220	;RK11=220
	CV RF,5,204	;RF11=204
	CV RC,5,210	;RC11=210
	CV DT,5,214	;DECTAPE=214
	CV RP,5,254	;RP11=254
	CV UDV,4,170	;USER DEVICE=170
	CV TTI2,4,KLVCTR+0	;TELETYPES 2-8
	CV TTO2,4,KLVCTR+4
	CV TTI3,4,KLVCTR+10
	CV TTO3,4,KLVCTR+14
	CV TTI4,4,KLVCTR+20
	CV TTO4,4,KLVCTR+24
	CV TTI5,4,KLVCTR+30
	CV TTO5,4,KLVCTR+34
	CV TTI6,4,KLVCTR+40
	CV TTO6,4,KLVCTR+44
	CV TTI7,4,KLVCTR+50
	CV TTO7,4,KLVCTR+54
	CV TTI8,4,KLVCTR+60
	CV TTO8,4,KLVCTR+64
	CV DMI,4,DMVCTR	;DM11, INPUT SIDE
	CV DMO,4,DMVCTR+4	;DM11, OUTPUT SIDE
	CV AD,6,130		;AD, AS PLUG WIRED
	CV AD7,7,130		;AD, PROGRAM SELECTABLE BR7


	END
