	TITLE	GETFAB - DYNAMIC ALLOCATION OF FAB
	HISEG

;GETFAB MOVES UP THE WHOLE BCT FAR ENOGH TO OPEN UP SPACE FOR A NEW FAB
;A POINTER TO THE FAB IS DEPOSITED IN THE APPROPRIATE WORD OF
;THE UCB POINTED AT BY .UCBAD
;THE ALLOCATED FAB WILL CONTAIN GARBAGE, SO ANYONE PLANNING TO USE
;IT WILL HAVE TO SET IT UP
;GETFAB MAY EXPAND MIMIC'S LOW (IMPURE) SEGMENT BY 1K IF ENOUGH
;SPACE FOR THE FAB DOESN'T EXIST
;THERE IS AT PRESENT NO WAY TO RECOVER SPACE ALLOCATED TO A FAB

	SUBTTL	BCTMOV - SUBROUTINE FOR MOVING THE BCT
	INTERN	GETCOR,BCTMOV
	EXTERN	JOBREL,JOBFF
	EXTERN	BCTTOP,BCTLIM,ERROR
	EXTERN	SMGR		;TO KEEP P.MAC HAPPY

;
;FOR BCTMOVE, AC2 IS NUMBER OF WORDS TO BE MOVED
;AC3 IS LOWEST WORD TO BE MOVED
;
AC1=1
AC2=2
AC3=3
FROM=4
TO=5
DELTA=AC2
HOLE=AC3
STK=17

	PAGE


BCTMOV:	JUMPE	DELTA,NONEWF	;DONT WASTE TIME MOVING NOTHING
	MOVE	FROM,BCTTOP	;GET BCTTOP
	ADDM	DELTA,BCTTOP	;UPDATE BCTTOP
	MOVE	AC1,BCTTOP	;GET UPDATED BCTTOP
	HRRM	AC1,JOBFF	;USE TO UPDATE JOBFF (1ST FREE WORD OF LOW SEG)
	CAML	AC1,BCTLIM	;IS THERE ROOM FOR IT?
	PUSHJ	STK,GETCOR	;NO, GET ENOUGH
	SUBI	FROM,1		;COMPUTE 1ST WORD TO BE MOVED
MOVELP:	CAMGE	FROM,HOLE	;ALL DONE?
	POPJ	STK,		;YES, RETURN
	MOVE	AC1,0(FROM)
	MOVE	TO,FROM
	ADD	TO,DELTA
	MOVEM	AC1,0(TO)	;THERE MUST BE A BETTER WAY
	SOJA	FROM,MOVELP

GETCOR:	MOVE	AC1,BCTTOP	;CORE UUO WOULD CLOBBER BCTTOP
	CALLI	AC1,11		;DO CORE UUO
	PUSHJ	STK,@ERROR
	HRRZ	AC1,JOBREL	;GET NEW END OF LOW SEG
	MOVEM	AC1,BCTLIM
	POPJ	STK,		;AND RETRUN

	PAGE
	SUBTTL	GETFAB
	INTERN	GETFAB
	EXTERN	BCTBOT

GETFAB:	MOVE	AC1,.UCBAD	;GET ADDRESS OF UCB DESIROUS OF FAB
	HLRZ	AC1,UFAB(AC1)	;GET ITS FAB POINTER
	JUMPN	AC1,NONEWF	;IF FAB ALREADY THERE, DONT ALLOCATE
	MOVEI	AC2,FABSIZ	;OPEN A HOLE OF FAB SIZE
	MOVE	AC3,BCTBOT	;AT THE BEGINNING OF THE BCT
	PUSHJ	STK,BCTMOV
	MOVE	AC1,BCTBOT	;BCTBOT NOW POINTS AT FAB
	MOVE	AC2,.UCBAD	;GET POINTER TO UCB FOR FAB
	HRLM	AC1,UFAB(AC2)	;PUT FAB POINTER INTO PROPER PLACE IN UCB
	ADDI	AC1,FABSIZ	;UPDATE BCTBOT
	MOVEM	AC1,BCTBOT
NONEWF:	POPJ	STK,		;AND RETURN


	END
